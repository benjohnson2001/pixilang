fn lp($freq, $r, $amp) {
    $bq = biquad(0, $freq, $r, $amp)
    ret ($bq.init($bq))
}

fn hp($freq, $r, $amp) {
    $bq = biquad(1, $freq, $r, $amp)
    ret ($bq.init($bq))
}

fn bp($freq, $r, $amp) {
    $bq = biquad(2, $freq, $r, $amp)
    ret ($bq.init($bq))
}

fn notch($freq, $r, $amp) {
    $bq = biquad(3, $freq, $r, $amp)
    ret ($bq.init($bq))
}

fn biquad($type, $freq, $r, $amp) {
    $this = new()

    $this.Type = $type
    $this.Freq = $freq
    $this.Amp = $amp

    $this._alpha = (1 - $r * $r) / 2

    $this._a = new(3, 1, FLOAT32) //feedforward filter coefficients
    $this._b = new(2, 1, FLOAT32) //feedback filter coefficients
    $this._f = new_filter()

    $this.init = {
        $this = $1

        $omega = 2 * M_PI * $this.Freq / get_audio_sample_rate(1)
        $tsin = sin($omega)
        $tcos = cos($omega)
        $alpha = $this._alpha

        $type = $this.Type

        if $type == 0 {
            // lp
            $b0 = (1 - $tcos)/ 2
            $b1 = 1 - $tcos
            $b2 = (1 - $tcos)/ 2
            $a0 = 1 + $alpha
            $a1 = -2 * $tcos
            $a2 = 1 - $alpha
        }

        if $type == 1 {
            // hp
            $b0 = (1 + $tcos)/ 2
            $b1 = -(1 + $tcos)
            $b2 = (1 + $tcos)/ 2
            $a0 = 1 + $alpha
            $a1 = -2 * $tcos
            $a2 = 1 - $alpha
        }

        if $type == 2 {
            // bp
            $b0 = $alpha
            $b1 = 0
            $b2 = -$alpha
            $a0 = 1 + $alpha
            $a1 = -2 * $tcos
            $a2 = 1 - $alpha
        }

        if $type == 3 {
            // notch
            $b0 = 1
            $b1 = -2 * $tcos
            $b2 = 1
            $a0 = 1 + $alpha
            $a1 = -2 * $tcos
            $a2 = 1 - $alpha
        }

        if $type == 4 {
            // ap
            $b0 = 1 - $alpha
            $b1 = -2 * $tcos
            $b2 = 1 + $alpha
            $a0 = 1 + $alpha
            $a1 = -2 * $tcos
            $a2 = 1 - $alpha
        }

        $this._a[0] = $b0 / $a0
        $this._a[1] = $b1 / $a0
        $this._a[2] = $b2 / $a0
        $this._b[0] = -$a1 / $a0
        $this._b[1] = -$a2 / $a0

        init_filter($this._f, $this._a, $this._b)

        ret ($this)
    }

    $this.gen = { // $this, $frames, $in_channels
        $this = $1
        $frames = $2
        $in_channels = $3

        $sig = new($frames, 1, FLOAT32)
        apply_filter($this._f, $sig, $this.buf, 0, 0, $frames)
        op_cn(OP_MUL, $sig, $this.Amp / $this._alpha)

        ret ($sig)
    }

    ret ($this)
}
