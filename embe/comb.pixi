fn comb($freq, $fbk) {
    $this = new()

    $this.Freq = $freq
    $this.Fbk = $fbk
    
    $srate = get_audio_sample_rate(1)
    $this._dly_line = new($srate / 2, 1, FLOAT32)

    $this.join = { // $this, $sig
        $this = $1
        $sig = $2

        $this.signal = $sig
    }

    $this.gen = { // $this, $frames, $in_channels
        $this = $1
        $frames = $2
        $in_channels = $3

        if $this.Buf == 0 {
            $this.Buf = new($frames, 1, FLOAT32)
        }

        $this.signal.gen($this.signal, $frames, $in_channels)
        copy($this.Buf, $this.signal.Buf)
        
        $srate = get_audio_sample_rate(1)

        $dly = $srate / $this.Freq
        if $dly > $frames {
            $dly = $frames
        }
        $dly0 = floor($dly)

        $i = 0 while ($i < $frames) {
            $dly_val = $this._dly_line[$i + $frames - $dly0]
            $dly_val1 = $this._dly_line[$i + 1 + $frames - $dly0]
            $this.Buf[$i] + ($dly_val + ($dly_val1 - $dly_val) * ($dly - $dly0)) * $this.Fbk
            $this._dly_line[$i + $frames] = $this.Buf[$i]
            $i + 1
        }
        copy($this._dly_line, $this._dly_line, 0, $frames)
    }

    ret ($this)
}
