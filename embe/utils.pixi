include "list.pixi"

fn in($channel_id) {
    $this = new()
    $this.channel_id = $channel_id

    $this.gen = { // $this, $channels, $frames, $in_channels
        $this = $1
        $channels = $2
        $frames = $3
        $in_channels = $4

        if $in_channels >= 0 {
            copy($channels[0], $in_channels[$this.channel_id])
            ret (1)
        } else {
            ret (0)
        }
    }

    ret ($this)
}

fn mix() {
    $this = new()
    $this.signals = -1
    $this.counter = 0

    $this.join = { // $this, $sig
        $this = $1
        $sig = $2
        $this.counter + 1
        $this.signals = _($sig, $this.signals)
    }

    $this.gen = { // $this, $channels, $frames, $in_channels
        $this = $1
        $channels = $2
        $frames = $3
        $in_channels = $4

        clean($channels[0])

        $ch = new()
        $ch[0] = new($frames, 1, FLOAT32)

        $signals = $this.signals
        $i = 0 while $i < $this.counter {
            $i + 1
            $sig = $signals.Head
            $signals = $signals.Tail

            $sig.gen($sig, $ch, $frames, $in_channels)
            op_cc(OP_ADD, $channels[0], $ch[0], $frames)
        }

        remove($ch[0])
        remove($ch)
    }

    ret ($this)
}

fn j($proc, $sig) {
    $proc.join($proc, $sig)
}
